; Astralis Core v0 grammar accepted by seed0 (EBNF-ish)
; Indentation establishes blocks. `connector` tokens (`->`, `as`, `:`, `then`)
; permit inline bodies; otherwise a NEWLINE is followed by an indented block.

program        = { statement [ NEWLINE ] } EOF ;

statement      = io_stmt
               | decl_stmt
               | return_stmt
               | break_stmt
               | continue_stmt
               | if_stmt
               | loop_stmt
               | repeat_stmt
               | try_stmt
               | func_def
               | expr_stmt
               ;

decl_stmt      = ("set" | "lock") IDENT "to" expr ;

io_stmt        = ("show" | "say" | "warn") expr ;

return_stmt    = "return" [ expr ] ;
break_stmt     = "break" ;
continue_stmt  = "continue" ;

if_stmt        = "if" expr [ connector ] inline_or_block [ "otherwise" [ connector ] inline_or_block ] ;

loop_stmt      = "loop" [ "forever" ] [ connector ] inline_or_block ;

repeat_stmt    = "repeat" IDENT "from" expr "to" expr [ connector ] inline_or_block ;

try_stmt       = "try" [ connector ] inline_or_block [ "otherwise" [ connector ] inline_or_block ] ;

func_def       = "define" IDENT "(" [ params ] ")" [ connector ] inline_or_block ;
params         = IDENT { "," IDENT } ;

expr_stmt      = expr ;

inline_or_block = inline_stmt | indented_block ;
inline_stmt    = statement ;
indented_block = NEWLINE INDENT { statement NEWLINE } DEDENT ;

expr           = add ;
add            = mult { ("+" | "-") mult } ;
mult           = unary { ("*" | "/") unary } ;
unary          = ("-" | "not") unary | conditional ;
conditional    = or_expr [ "if" or_expr "otherwise" or_expr ] ;
or_expr        = and_expr { "or" and_expr } ;
and_expr       = equality { "and" equality } ;
equality       = compare { ("==" | "!=") compare } ;
compare        = call { ("<" | "<=" | ">" | ">=") call } ;
call           = primary { "(" [ args ] ")" } ;
args           = expr { "," expr } ;
primary        = NUMBER
               | STRING
               | IDENT       ;      ; includes `ask` keyword for now

connector      = "->" | "as" | ":" | "then" ;
